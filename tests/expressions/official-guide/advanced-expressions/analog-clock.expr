# Construct 3 linearly increasing waves, one for each hand
 extent = (fraction % (1/3)) * 100;

# Convert the seconds to radians on a clock
 angle = -floor(time) * pi * 2 ;

# Calculate the angle of each hand
 secondAng = angle / 60 + pi/2;
 minuteAng = angle / (60^2) + pi/2;
 hourAng = angle / (60^2 * 12) + pi/2;

# Plot out the lines for each clock hand
 secondX = cos(secondAng) * extent;
 secondY = sin(secondAng) * extent;

 minuteX = cos(minuteAng) * extent * .8;
 minuteY = sin(minuteAng) * extent * .8;

 hourX = cos(hourAng) * extent * 0.5;
 hourY = sin(hourAng) * extent * 0.5;

# Choose the colors for each of the hands
 secondH = extent;
 minuteH = 100 + extent;
 hourH = 200 + extent;

# Check if the current beam should be a part of the second, minute, or hour hand
 isSecond = fraction < (1/3);
 isMinute = if(isSecond, 0, fraction < (2/3));

# Select the position based on the criteria above
 x' = if(isSecond, secondX, if(isMinute, minuteX, hourX));
 y' = if(isSecond, secondY, if(isMinute, minuteY, hourY));

# And the same for the color
 h = if(isSecond, secondH, if(isMinute, minuteH, hourH));
 s = 1;
 v = 1;